plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.williambl.buskymore'
version = '1.0.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation("net.dv8tion:JDA:5.3.0") {
        exclude module: 'opus-java'
    }
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation "com.github.mizosoft.methanol:methanol:1.8.2"
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

tasks.register('copyJarToStaticName', Copy) {
    from jar
    into layout.buildDirectory.dir('static')
    with {
        rename { String filename ->
            filename.replace("-$project.version", "")
        }
    }
}

assemble.finalizedBy(copyJarToStaticName)

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "willBlRepositoryReleases"
            url = "https://maven.willbl.dev/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
